plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.8'
}

group = 'fr.mediavee'
version = '1.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT"
    implementation "com.moandjiezana.toml:toml4j:0.7.2"

    testImplementation "com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT"
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

test {
    useJUnitPlatform()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    enableRelocation = true
    relocationPrefix = group + '.' + project.name + '.libs'
}

jar {
    dependsOn shadowJar
}

build {
    dependsOn shadowJar
    finalizedBy 'copyToTarget'
}

clean {
    delete "$rootDir/target"
}

tasks.register('copyToTarget', Copy) {
    dependsOn shadowJar
    from(shadowJar.archiveFile)
    into("$rootDir/target")
    rename { fileName ->
        return "${project.name}-${version}.jar"
    }
}